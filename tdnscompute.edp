//
// tdnscompute.edp
// Chris Douglas
// cdoug@mit.edu
//
// EXAMPLE USAGE:
//  Compute time-domain nonlinear simulation with IC from input file without mesh adaptation:
// mpirun -n 4 FreeFem++-mpi -v 0 tdnscompute.edp -fi <FILEIN> -fo <FILEOUT>
//
//  Compute time-domain nonlinear simulation with IC from input file with mesh adaptation:
// mpirun -n 4 FreeFem++-mpi -v 0 tdnscompute.edp -fi <FILEIN> -fo <FILEOUT> -mo <MESHOUT>
//
// NOTE: This file should not be changed unless you know what you're doing.
//
load "iovtk"
load "PETSc"
include "settings.idp"
include "macros_bifbox.idp"
// arguments
string meshin = getARGV("-mi", ""); // input meshfile with extension
string meshout = getARGV("-mo", "");
string filein = getARGV("-fi", "");
string fileout = getARGV("-fo", filein);
int count = getARGV("-count", 0);
int savecount = getARGV("-scount", 1);
int maxcount = getARGV("-maxcount", 100);
real dt = getARGV("-dt", 0.01);
string sneslinesearchtype = getARGV("-snes_linesearch_type","basic");

// Load mesh, make FE basis
string fileroot, meshroot;
string fileext = parsefilename(filein, fileroot); //extract file name and extension
parsefilename(fileout, fileout); // trim extension from output file, if given
if(fileext == "mode" || fileext == "rslv"){
  filein = readbasename(workdir + filein);
  fileext = parsefilename(filein, fileroot);
}
if(meshin == "") meshin = readmeshname(workdir + filein); // get mesh file
string meshext = parsefilename(meshin, meshroot);
parsefilename(meshout, meshroot); // trim extension from output mesh, if given
if(count > 0) {
  fileroot = fileroot(0:fileroot.rfind("_" + count)-1); // get file root
  meshroot = meshroot(0:meshroot.rfind("_" + count)-1); // get file root
}
Th = readmeshN(workdir + meshin);
Thg = Th;
buildDmesh(Th);
restu = restrict(XMh, XMhg, n2o);
XMh defu(ub), defu(um), defu(um2), defu(um3);
real time = 0.0;
if (count == 0){
  if(fileext == "base") {
    ub[] = loadbase(fileroot, meshin);
  }
  else if(fileext == "fold") {
    real[string] alpha;
    real beta;
    real[int] qm, qma;
    ub[] = loadfold(fileroot, meshin, qm, qma, alpha, beta);
  }
  else if(fileext == "hopf") {
    int sym;
    real omega;
    complex[string] alpha;
    complex beta;
    complex[int] qm, qma;
    ub[] = loadhopf(fileroot, meshin, qm, qma, sym, omega, alpha, beta);
  }
  else if(fileext == "foho") {
    int sym;
    real omega;
    complex[string] alpha1;
    real[string] alpha2;
    complex beta1, gamma12, gamma13;
    real beta22, beta23, gamma22, gamma23;
    complex[int] q1m, q1ma;
    real[int] q2m, q2ma;
    ub[] = loadfoho(fileroot, meshin, q1m, q1ma, q2m, q2ma, sym, omega, alpha1, alpha2, beta1, beta22, beta23, gamma12, gamma13, gamma22, gamma23);
  }
  else if(fileext == "hoho") {
    int sym1, sym2;
    real omega1, omega2;
    complex[string] alpha1, alpha2;
    complex beta1, beta2, gamma1, gamma2, gamma12, gamma13, gamma22, gamma23;
    complex[int] q1m, q1ma, q2m, q2ma;
    ub[] = loadhoho(fileroot, meshin, q1m, q1ma, q2m, q2ma, sym1, sym2, omega1, omega2, alpha1, alpha2, beta1, beta2, gamma1, gamma2, gamma12, gamma13, gamma22, gamma23);
  }
  else if(fileext == "tdns") {
    real time;
    ub[] = loadtdns(fileroot, meshin, time);
  }
  else if(fileext == "porb") {
    int sym, Nh;
    real omega;
    complex[int, int] qh(um[].n, Nh);
    ub[] = loadporb(fileroot, meshin, qh, sym, omega, Nh);
    for (int nh = 0; nh < Nh; nh++) ub[] += (2.0*qh(:,nh).re);
  }
  else {
    defu(ub) = InitialConditions;
    setparams(paramnames, params);
  }
  savetdns(filein, (savecount > 0 ? fileout : ""), meshin, filein, time, false, false);
}
else {
  ub[] = loadtdns(fileroot + "_" + count, meshin, time);
}
// Create distributed Mat
Mat J;
createMatu(Th, J, Pk);
int sym = 0;
real ik = 0.0, ik2 = 0.0, ik3 = 0.0, iomega = 1.0/dt, iomega2 = 0.0, iomega3 = 0.0;
include "eqns.idp" // load equations
bool adapt = false;
if(meshout != "")  adapt = true;  // if output meshfile is given, adapt mesh
meshout = meshin; // if no adaptation
// initialize
real[int] q, qp;
ChangeNumbering(J, ub[], q);
ChangeNumbering(J, ub[], qp);
// Function to build residual "F(t,u,\dot{u})" operator in PETSc numbering
func real[int] funcF(real[int]& qPETSc) {
    ChangeNumbering(J, ub[], qPETSc, inverse = true, exchange = true);
    ChangeNumbering(J, um[], qp, inverse = true, exchange = true);
    um[] -= ub[]; // -dq
    um[] /= -dt; // -dq/-dt = dq/dt
    real[int] FPETSc, F = vR(0, XMh, tgv = -1);
    real[int] M = vM(0, XMh, tgv = -10);
    F += M;
    ChangeNumbering(J, F, FPETSc);
    return FPETSc;
}
// Function to build Jacobian operator in PETSc numbering
func int funcJ(real[int]& qPETSc) {
    ChangeNumbering(J, ub[], qPETSc, inverse = true, exchange = true);
    iomega = 1.0/dt;
    J = vJ(XMh, XMh, tgv = -1);
    return 0;
}
// set up Mat parameters
IFMACRO(Jprecon) Jprecon(0); ENDIFMACRO
set(J, IFMACRO(Jsetargs) Jsetargs, ENDIFMACRO sparams = KSPparams);
while (count < maxcount){
  int ret;
  // solve nonlinear problem with TS
  SNESSolve(J, funcJ, funcF, q, reason = ret,
            sparams = "-snes_linesearch_type " + sneslinesearchtype + " -options_left no -snes_converged_reason"); // solve nonlinear problem with SNES
  if(ret > 0) { // Save solution if solver converged and output file is given
    count++;
    time += dt;
    if(adapt && (count % savecount == 0)) { // if output meshfile is given, adapt mesh
      ChangeNumbering(J, ub[], q, inverse = true);
      ChangeNumbering(J, um[], qp, inverse = true);
      meshout = meshroot + "_" + count + "." + meshext;
      XMhg defu(uG), defu(umG), defu(tempu); // create private global FE functions
      tempu[](restu) = ub[]; // populate local portion of global soln
      mpiAllReduce(tempu[], uG[], mpiCommWorld, mpiSUM); //aggregate local solns into global soln
      tempu[](restu) = um[]; // populate local portion of prev. global soln
      mpiAllReduce(tempu[], umG[], mpiCommWorld, mpiSUM); //aggregate local solns into global soln
      if(mpirank == 0) { // Perform mesh adaptation (serially) on processor 0
        IFMACRO(dimension,2)
          Thg = adaptmesh(Thg, adaptu(uG), adaptmeshoptions);
        ENDIFMACRO
        IFMACRO(dimension,3)
          cout << "NOTE: 3D mesh adaptation is still under development." << endl;
          load "mshmet"
          load "mmg"
          real anisomax = getARGV("-anisomax",1.0);
          real[int] met = mshmet(Thg, adaptu(uG), normalization = getARGV("-normalization",1), aniso = bool(anisomax > 1.0), hmin = getARGV("-hmin", 1.0e-6), hmax = getARGV("-hmax", 1.0e+2), err = getARGV("-err", 1.0e-2));
          if(anisomax > 1.0) {
            load "aniso"
            boundaniso(6, met, anisomax);
          }
          Thg = mmg3d(Thg, metric = met, hmin = getARGV("-hmin", 1.0e-6), hmax = getARGV("-hmax", 1.0e+2), hgrad = -1, verbose = verbosity-(verbosity==0));
        ENDIFMACRO
      }
      broadcast(processor(0), Thg); // broadcast global mesh to all processors
      defu(uG) = defu(uG); //interpolate global solution from old mesh to new mesh
      defu(umG) = defu(umG); //interpolate global solution from old mesh to new mesh
      Th = Thg; //Reinitialize local mesh with global mesh
      Mat Adapt;
      createMatu(Th, Adapt, Pk); // Partition new mesh and update the PETSc numbering
      J = Adapt;
      IFMACRO(Jprecon) Jprecon(0); ENDIFMACRO
      set(J, IFMACRO(Jsetargs) Jsetargs, ENDIFMACRO sparams = KSPparams);
      defu(ub) = initu(0.0); // set local values to zero
      defu(um) = initu(0.0); // set local values to zero
      restu.resize(ub[].n); // Change size of restriction operator
      restu = restrict(XMh, XMhg, n2o); // Compute new restriction from global mesh to local mesh
      ub[] = uG[](restu); //restrict global solution to each local mesh
      um[] = umG[](restu); //restrict global solution to each local mesh
      Mat Adapt0(J);
      q.resize(J.n);  // Resize state vector
      qp.resize(J.n);  // Resize state vector
      ChangeNumbering(J, ub[], q);
      ChangeNumbering(J, um[], qp);
      SNESSolve(J, funcJ, funcF, q, reason = ret,
                sparams = "-snes_linesearch_type " + sneslinesearchtype + " -snes_converged_reason -options_left no"); // solve nonlinear problem with SNES
      assert(ret > 0);
      if(mpirank == 0) { // Save adapted mesh
        cout << "  Saving adapted mesh '" + meshout + "' in '" + workdir + "'." << endl;
        savemesh(Thg, workdir + meshout);
      }
    }
    ChangeNumbering(J, ub[], q, inverse = true);
    savetdns(fileout + "_" + count, (savecount > 0 ? fileout : ""), meshout, filein, time, (count % savecount == 0), true);
    qp = q;
    IFMACRO(Jprecon) Jprecon(0); ENDIFMACRO
  }
  else {
    if(mpirank == 0) cout << "\tConvergence failed. Reattempting with halved time step." << endl;
    q = qp;
    dt /= 2.0;
  }
}
