//
// cylinder.edp
// Chris Douglas
// chris.douglas@ladhyx.polytechnique.fr
//
// This file can be used with FreeFEM to create a mesh for the cylinder as in
// [D. Sipp and A. Lebedev, JFM (2007) DOI:10.1017/S0022112007008907].
//

assert(mpisize == 1); // Must be run with 1 processor
include "settings_sipp_lebedev_2007.idp"
real n0 = 25;
real n1 = 2.5;
real n2 = 0.25;
real xmin = -60.0;
real xmax = 200.0;
real rmax = 30.0;
real rcyl = 0.5;

string meshout = getARGV("-mo", "cylinder.msh"); // mesh filename
if(meshout.rfind(".msh") < 0) meshout = meshout + ".msh"; // add extension if not provided

// Define borders
//  o------------------6-------------------o
//  |                                      |
//  1                                      5
//  |           ╭--3--╮                    |
//  o-----2-----o  0  o---------4----------o
border C01(t=0, 1){x=xmin; y=rmax*(1-t); label=BCinflow;}
border C02(t=0, 1){x=xmin+(abs(xmin)-rcyl)*t; y=0; label=BCaxis;}
border C03(t=0, 1){x=-rcyl*cos(pi*t); y=rcyl*sin(pi*t); label=BCwall;}
border C04(t=0, 1){x=rcyl+(xmax-rcyl)*t; y=0; label=BCaxis;}
border C05(t=0, 1){x=xmax; y=rmax*t; label=BCopen;}
border C06(t=0, 1){x=xmax+(xmin-xmax)*t; y=rmax; label=BCslip;}
// Assemble mesh
mesh Thg = buildmesh(C01(n2*rmax) + C02(n1*(0.5-xmin)) + C03(n0*pi*rcyl) + C04(n1*(xmax-0.5)) + C05(n2*rmax) + C06(n2*(xmax-xmin)));

int[int] meshlabels = labels(Thg);
cout << "\tMesh: " << Thg.nv << " vertices, " << Thg.nt << " triangles, " << Thg.nbe << " boundary elements, " << meshlabels.n << " labeled boundaries." << endl;
cout << "  Saving mesh '" + meshout + "'." << endl;
savemesh(Thg, meshout);
