//
// cylinder.edp
// Chris Douglas
// chris.douglas@ladhyx.polytechnique.fr
//
// This file can be used with FreeFEM to create a mesh for the cylinder.
//

assert(mpisize == 1); // Must be run with 1 processor
include "settings.idp"
real n0 = 50;
real n1 = 10;
real xmin = -5.0;
real xmax = 10.0;
real rmax = 0.5;
real rcyl = 0.25;

string meshout = getARGV("-mo", "cylinder.msh"); // mesh filename
if(meshout.rfind(".msh") < 0) meshout = meshout + ".msh"; // add extension if not provided

// Define borders
//  o------------------6-------------------o
//  |                                      |
//  1                                      5
//  |           ╭--3--╮                    |
//  o-----2-----o  0  o---------4----------o
border C01(t=0, 1){x=xmin; y=rmax*(1-t); label=BCinflow;}
border C02(t=0, 1){x=xmin-(xmin+rcyl)*t; y=0; label=BCaxis;}
border C03(t=0, 1){x=-rcyl*cos(pi*t); y=rcyl*sin(pi*t); label=BCcyl;}
border C04(t=0, 1){x=rcyl+(xmax-rcyl)*t; y=0; label=BCaxis;}
border C05(t=0, 1){x=xmax; y=rmax*t; label=BCopen;}
border C06(t=0, 1){x=xmax+(xmin-xmax)*t; y=rmax; label=BCwall;}
// Assemble mesh
mesh Thg = buildmesh(C01(n1*rmax) + C02(n1*(rcyl-xmin)) + C03(n0*pi*rcyl) + C04(n1*(xmax-rcyl)) + C05(n1*rmax) + C06(n1*(xmax-xmin)));

int[int] meshlabels = labels(Thg);
cout << "\tMesh: " << Thg.nv << " vertices, " << Thg.nt << " triangles, " << Thg.nbe << " boundary elements, " << meshlabels.n << " labeled boundaries." << endl;
cout << "  Saving mesh '" + meshout + "'." << endl;
savemesh(Thg, meshout);
