//
// eqns_meliga_etal_2012.idp
// Chris Douglas
// cdoug@mit.edu
//
// Define KSP parameters
  string KSPparams = "-ksp_type preonly -pc_type lu";
// term macros ------------------------------------------------------
  macro zeta(a, b) (0.5 + 0.5*tanh(4.0*tan(-0.5*pi+pi*abs((a)-(b))/50.0)))//EOM

  macro Retilde()(1.0/params["1/Re"] + (x > 70.0)*(0.1 - 1.0/params["1/Re"])*zeta(x, 70.0)
                               + (y > 10.0)*((0.1 - (1.0/params["1/Re"] + (x > 70.0)*(0.1 - 1.0/params["1/Re"])*zeta(x, 70.0)))*zeta(y, 10.0)))//EOM

  macro vdotu(v, u) ( y*(v*u + v#y*u#y + v#z*u#z) ) //EOM

  macro div(ik, u) ( y*(dx(u) + dy(u#y)) + u#y + (ik)*u#z ) // velocity divergence

  macro ugradu(v, U, ik, u) (  v  *(y*(U*dx(u  ) + U#y*dy(u  )) + U#z* (ik)*u          )
                             + v#y*(y*(U*dx(u#y) + U#y*dy(u#y)) + U#z*((ik)*u#y - u#z))
                             + v#z*(y*(U*dx(u#z) + U#y*dy(u#z)) + U#z*((ik)*u#z + u#y)) ) // scaled convection term

  macro visc(ij, v, ik, u) ( y*(dx(v)*dx(u) + dy(v)*dy(u) + dx(v#y)*dx(u#y) + dy(v#y)*dy(u#y) + dx(v#z)*dx(u#z) + dy(v#z)*dy(u#z))
                            + ((ij)*v*(ik)*u + ((ij)*v#y - v#z)*((ik)*u#y - u#z) + ((ij)*v#z + v#y)*((ik)*u#z + u#y))/y ) // EOM
// Boundary conditions
  macro BoundaryConditions(u, U)
    on(BCinflow, u = U - 1.0, u#y = U#y, u#z = U#z - params["S"]*((y <= 1.0)*y*(2.0 - y^2) + (y > 1.0)/(y+(y == 0))))
    + on(BCslip, u#y = U#y, u#z = U#z)
    + on(BCaxis, u#y = U#y, u#z = U#z)
  // EOM
  macro HomBoundaryConditions(u)
    on(BCinflow, u = 0, u#y = 0, u#z = 0)
    + on(BCslip, u#y = 0, u#z = 0)
    + on((abs(int(sym)) != 1)*BCaxis, u#y = 0, u#z = 0)
    + on((abs(int(sym)) >  0)*BCaxis, u = 0)
  // EOM
// RESIDUAL OPERATOR
  varf vR(defu(dum), defu(v))
    = int2d(Th)(
      ugradu(v, ub, 0, ub) - div(0, v)*ubp + visc(0, v, 0, ub)/Retilde
      - vp*div(0, ub)
    )
    + BoundaryConditions(dum, ub);

// JACOBIAN OPERATOR
  varf vJ(defu(dum), defu(v))
    = int2d(Th)(
      iomega*vdotu(v, dum)
      + ugradu(v, dum, 0, ub) + ugradu(v, ub, ik, dum) - div(-ik, v)*dump + visc(-ik, v , ik, dum )/Retilde
      - vp*div(ik, dum)
    )
    + int2d(Th)(
      iomega*vdotu(v, um)
      + ugradu(v, um, 0, ub) + ugradu(v, ub, ik, um) - div(-ik, v)*ump + visc(-ik, v , ik, um )/Retilde
      - vp*div(ik, um)
    )
    + HomBoundaryConditions(dum);

// MASS OPERATOR
  varf vM(defu(dum), defu(v))
    = int2d(Th)( vdotu(v, dum) )
    + int2d(Th)( vdotu(v, um) )
    + HomBoundaryConditions(dum);

// FORCING/RESPONSE WEIGHTING MATRICES (FOR RESOLVENT ANALYSIS)
  varf vMq(defu(dum), defu(v))
    = int2d(Th)( vdotu(v, dum) );

  varf vMf(deff(fm), deff(v))
    = int2d(Th)( vdotu(v, fm) );

  varf vP(deff(fm), defu(v))
    = int2d(Th)( vdotu(v, fm) );

// HESSIAN OPERATOR
  varf vH(defu(dum), defu(v))
    = int2d(Th)(
      ugradu(v, um, ik2, um2) + ugradu(v, um2, ik, um)
    )
    + int2d(Th)(
      ugradu(v, um, ik2, dum) + ugradu(v, dum, ik, um)
    )
    + HomBoundaryConditions(dum);

// TRESSIAN OPERATOR
  varf vT(defu(dum), defu(v))
    = HomBoundaryConditions(dum);
