//
// eqns_douglas_etal_2022.idp
// Chris Douglas
// christopher.douglas@duke.edu
//
// Define KSP parameters
  string KSPparams = "-ksp_type preonly -pc_type lu";
// term macros ------------------------------------------------------
  macro ndotu(u) (N.x*u*dYdy + N.y*u#y) //EOM

  macro tdotgrads(s) ( -N.y*dx(s) + N.x*dy(s) ) //EOM NOTE: grid scaling factors cancel

  macro vdotu(v, u) ( Y*(v*u + v#y*u#y + v#z*u#z) ) //EOM

  macro div(ik, u) ( Y*(dx(u) + dY(u#y)) + u#y + (ik)*u#z ) // velocity divergence

  macro ugradu(v, U, ik, u) (  v  *(Y*(U*dx(u  ) + U#y*dY(u  )) + U#z* (ik)*u          )
                             + v#y*(Y*(U*dx(u#y) + U#y*dY(u#y)) + U#z*((ik)*u#y - u#z))
                             + v#z*(Y*(U*dx(u#z) + U#y*dY(u#z)) + U#z*((ik)*u#z + u#y)) ) // scaled convection term

  macro visc(ij, v, ik, u) ( Y*(dx(v)*dx(u) + dY(v)*dY(u) + dx(v#y)*dx(u#y) + dY(v#y)*dY(u#y) + dx(v#z)*dx(u#z) + dY(v#z)*dY(u#z))
                            + ((ij)*v*(ik)*u + ((ij)*v#y - v#z)*((ik)*u#y - u#z) + ((ij)*v#z + v#y)*((ik)*u#z + u#y))/Y ) // EOM
// Boundary conditions
  macro BoundaryConditions(u, U)
    on(BCinflow, u = U - 2.0*(1.0 - 4.0*Y^2 - log(2.0*Y)*(1.0 - params["d"]^2)/log(params["d"]))/(1.0 + params["d"]^2 + (1.0 - params["d"]^2)/log(params["d"])), u#z = U#z - 2.0*params["S"]*Y, u#po = U#po)
    + on(BCwall, u = U, u#y = U#y, u#z = U#z, u#po = U#po)
    + on(BCpipe, u = U, u#y = U#y, u#z = U#z - 2.0*params["S"]*Y, u#po = U#po)
    + on(BCaxis, u#y = U#y, u#z = U#z)
  // EOM
  macro HomBoundaryConditions(u)
    on(BCinflow, u = 0, u#z = 0, u#po = 0)
    + on(BCwall, BCpipe, u = 0, u#y = 0, u#z = 0, u#po = 0)
    + on((abs(int(sym(0))) != 1)*BCaxis, u#y = 0, u#z = 0)
    + on((abs(int(sym(0))) >  0)*BCaxis, u = 0)
  // EOM
// RESIDUAL OPERATOR
  varf vR(defu(dum), defu(v))
    = int2d(Th)(
      Jinv*(
      ugradu(v, ub, 0, ub) - div(0, v)*ubp + visc(0, v, 0, ub)*params["1/Re"]
      - vp*div(0, ub)
      )
    )
    + int1d(Th, BCopen)(
      Y*ndotu(v)*ubpo - 0.5*vdotu(v, ub)*min(0.0, real(ndotu(ub)))
      + vpo*(Y*tdotgrads(ubpo) - tmag*ubz^2*N.x)
    )
    + BoundaryConditions(dum, ub);

// JACOBIAN OPERATOR
  varf vJ(defu(dum), defu(v))
    = int2d(Th)(
      Jinv*(
      iomega*vdotu(v, dum)
      + ugradu(v, dum, 0, ub) + ugradu(v, ub, ik(0), dum) - div(-ik(0), v)*dump + visc(-ik(0), v , ik(0), dum )*params["1/Re"]
      - vp*div(ik(0), dum)
      + 1e-10*vpo*dumpo
      )
    )
    + int1d(Th, BCopen)(
      Y*ndotu(v)*dumpo*(int(sym(0)) == 0) - 0.5*(real(ndotu(ub)) < 0)*(vdotu(v, dum)*ndotu(ub) + vdotu(v, ub)*ndotu(dum))
      + vpo*(Y*tdotgrads(dumpo) - 2.0*tmag*ubz*dumz*N.x)*(int(sym(0)) == 0)
    )
    + int2d(Th)(
      Jinv*(
      iomega*vdotu(v, um)
      + ugradu(v, um, 0, ub) + ugradu(v, ub, ik(0), um) - div(-ik(0), v)*ump + visc(-ik(0), v , ik(0), um )*params["1/Re"]
      - vp*div(ik(0), um)
      )
    )
    + int1d(Th, BCopen)(
      Y*ndotu(v)*umpo*(int(sym(0)) == 0) - 0.5*(real(ndotu(ub)) < 0)*(vdotu(v, um)*ndotu(ub) + vdotu(v, ub)*ndotu(um))
      + vpo*(Y*tdotgrads(umpo) - 2.0*tmag*ubz*umz*N.x)*(int(sym(0)) == 0)
    )
    + HomBoundaryConditions(dum);

// MASS OPERATOR
  varf vM(defu(dum), defu(v))
    = int2d(Th)( Jinv*vdotu(v, dum) )
    + int2d(Th)( Jinv*vdotu(v, um) )
    + HomBoundaryConditions(dum);

// FORCING/RESPONSE OPERATORS
  varf vMq(defu(dum), defu(v))
    = int2d(Th)( Jinv*vdotu(v, dum) )
    + int2d(Th)( Jinv*vdotu(v, um) )
    + HomBoundaryConditions(dum);

  varf vMf(deff(fm), deff(v))
    = int2d(Th)( Jinv*vdotu(v, fm) );

  varf vP(deff(fm), defu(v))
    = int2d(Th)( Jinv*vdotu(v, fm) );

// HESSIAN OPERATOR
  varf vH(defu(dum), defu(v))
    = int2d(Th)(
      Jinv*(
      ugradu(v, um, ik2(0), um2) + ugradu(v, um2, ik(0), um)
      )
    )
    + int1d(Th, BCopen)(
      -0.5*(real(ndotu(ub)) < 0)*(vdotu(v, um)*ndotu(um2) + vdotu(v, um2)*ndotu(um))
      - 2.0*tmag*vpo*um2z*umz*N.x*(int(sym(0)) == 0)
    )
    + int2d(Th)(
      Jinv*(
      ugradu(v, um, ik2(0), dum) + ugradu(v, dum, ik(0), um)
      )
    )
    + int1d(Th, BCopen)(
      -0.5*(real(ndotu(ub)) < 0)*(vdotu(v, um)*ndotu(dum) + vdotu(v, dum)*ndotu(um))
      - 2.0*tmag*vpo*dumz*umz*N.x*(int(sym(0)) == 0)
    )
    + HomBoundaryConditions(dum);

// TRESSIAN OPERATOR
  varf vT(defu(dum), defu(v))
    = HomBoundaryConditions(dum);
