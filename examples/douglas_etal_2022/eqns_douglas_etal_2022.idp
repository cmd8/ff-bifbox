//
// eqns_douglas_etal_2022.idp
// Chris Douglas
// cdoug@mit.edu
//
// Define KSP parameters
  string KSPparams = "-ksp_type preonly -pc_type lu";
// coordinate mapping macros ---------------------------------------------------
  macro Y() (
        (y < 0.5)*(params["d"]*y)
      + ((y >= 0.5) && (y <= 1.0))*(0.5*params["d"] + (1. - params["d"])*(y - 0.5))
      + (y > 1.0)*(y - 0.5)
      ) //EOM

  macro dydY() (
        (y < 0.5)*(1./params["d"])
      + ((y >= 0.5) && (y <= 1.0))*(1.0/(1. - params["d"]))
      + (y > 1.0)*(1.0)
      ) // EOM

  macro dY(a) ( dydY*dy(a) ) //EOM


  macro Jinv() (1./dydY) //EOM
  macro tmag() (sqrt((N.y)^2 + (N.x/dydY)^2)) //EOM
// term macros ------------------------------------------------------
  macro ndotu(u) (N.x*u/dydY + N.y*u#y) //EOM

  macro tdotgrads(s) ( -N.y*dx(s) + N.x*dy(s) ) //EOM NOTE: grid scaling factors cancel

  macro vdotu(v, u) ( Y*(v*u + v#y*u#y + v#z*u#z) ) //EOM

  macro div(ik, u) ( Y*(dx(u) + dY(u#y)) + u#y + (ik)*u#z ) // velocity divergence

  macro ugradu(v, U, ik, u) (  v  *(Y*(U*dx(u  ) + U#y*dY(u  )) + U#z* (ik)*u          )
                             + v#y*(Y*(U*dx(u#y) + U#y*dY(u#y)) + U#z*((ik)*u#y - u#z))
                             + v#z*(Y*(U*dx(u#z) + U#y*dY(u#z)) + U#z*((ik)*u#z + u#y)) ) // scaled convection term

  macro visc(ij, v, ik, u) ( Y*(dx(v)*dx(u) + dY(v)*dY(u) + dx(v#y)*dx(u#y) + dY(v#y)*dY(u#y) + dx(v#z)*dx(u#z) + dY(v#z)*dY(u#z))
                            + ((ij)*v*(ik)*u + ((ij)*v#y - v#z)*((ik)*u#y - u#z) + ((ij)*v#z + v#y)*((ik)*u#z + u#y))/Y ) // EOM
// Boundary conditions
  macro BoundaryConditions(u, U)
    on(BCinflow, u = U - 2.0*(1.0 - 4.0*Y^2 + (1.0 - params["d"]^2)/log(1.0/params["d"])*log(2.0*Y))/((1.0 + params["d"]^2) - (1.0 - params["d"]^2)/log(1.0/params["d"])), u#z = U#z - 2.0*params["S"]*Y, u#po = U#po)
    + on(BCwall, u = U, u#y = U#y, u#z = U#z, u#po = U#po)
    + on(BCpipe, u = U, u#y = U#y, u#z = U#z - 2.0*params["S"]*Y, u#po = U#po)
    + on(BCaxis, u#y = U#y, u#z = U#z)
  // EOM
  macro HomBoundaryConditions(sym, u)
    on(BCinflow, u = 0, u#z = 0, u#po = 0)
    + on(BCwall, BCpipe, u = 0, u#y = 0, u#z = 0, u#po = 0)
    + on((abs(sym) != 1)*BCaxis, u#y = 0, u#z = 0)
    + on((abs(sym) >  0)*BCaxis, u = 0)
  // EOM
// RESIDUAL OPERATOR
  varf vR(defu(dum), defu(v))
    = int2d(Th)(
      Jinv*(
      ugradu(v, ub, 0, ub) - div(0, v)*ubp + visc(0, v, 0, ub)*params["1/Re"]
      - vp*div(0, ub)
      )
    )
    + int1d(Th, BCopen)(
      Y*ndotu(v)*ubpo - 0.5*vdotu(v, ub)*min(0.0, real(ndotu(ub)))
      + vpo*(Y*tdotgrads(ubpo) - tmag*ubz^2*N.x)
    )
    + BoundaryConditions(dum, ub);

// JACOBIAN OPERATOR
  varf vJ(defu(dum), defu(v))
    = int2d(Th)(
      Jinv*(
      iomega*vdotu(v, dum)
      + ugradu(v, dum, 0, ub) + ugradu(v, ub, ik, dum) - div(-ik, v)*dump + visc(-ik, v , ik, dum )*params["1/Re"]
      - vp*div(ik, dum)
      + 1e-10*vpo*dumpo
      )
    )
    + int1d(Th, BCopen)(
      Y*ndotu(v)*dumpo*(sym == 0) - 0.5*(real(ndotu(ub)) < 0)*(vdotu(v, dum)*ndotu(ub) + vdotu(v, ub)*ndotu(dum))
      + vpo*(Y*tdotgrads(dumpo) - 2.0*tmag*ubz*dumz*N.x)*(sym == 0)
    )
    + int2d(Th)(
      Jinv*(
      iomega*vdotu(v, um)
      + ugradu(v, um, 0, ub) + ugradu(v, ub, ik, um) - div(-ik, v)*ump + visc(-ik, v , ik, um )*params["1/Re"]
      - vp*div(ik, um)
      )
    )
    + int1d(Th, BCopen)(
      Y*ndotu(v)*umpo*(sym == 0) - 0.5*(real(ndotu(ub)) < 0)*(vdotu(v, um)*ndotu(ub) + vdotu(v, ub)*ndotu(um))
      + vpo*(Y*tdotgrads(umpo) - 2.0*tmag*ubz*umz*N.x)*(sym == 0)
    )
    + HomBoundaryConditions(sym, dum);

// MASS OPERATOR
  varf vM(defu(dum), defu(v))
    = int2d(Th)( Jinv*vdotu(v, dum) )
    + int2d(Th)( Jinv*vdotu(v, um) )
    + HomBoundaryConditions(sym, dum);

// FORCING/RESPONSE WEIGHTING MATRICES (FOR RESOLVENT ANALYSIS)
  varf vMq(defu(dum), defu(v))
    = int2d(Th)( Jinv*vdotu(v, dum) );

  varf vMf(deff(fm), deff(v))
    = int2d(Th)( Jinv*vdotu(v, fm) );

  varf vP(deff(fm), defu(v))
    = int2d(Th)( Jinv*vdotu(v, fm) );

// HESSIAN OPERATOR
  varf vH(defu(dum), defu(v))
    = int2d(Th)(
      Jinv*(
      ugradu(v, um, ik2, um2) + ugradu(v, um2, ik, um)
      )
    )
    + int1d(Th, BCopen)(
      -0.5*(real(ndotu(ub)) < 0)*(vdotu(v, um)*ndotu(um2) + vdotu(v, um2)*ndotu(um))
      - 2.0*tmag*vpo*um2z*umz*N.x*(sym == 0)
    )
    + int2d(Th)(
      Jinv*(
      ugradu(v, um, ik2, dum) + ugradu(v, dum, ik, um)
      )
    )
    + int1d(Th, BCopen)(
      -0.5*(real(ndotu(ub)) < 0)*(vdotu(v, um)*ndotu(dum) + vdotu(v, dum)*ndotu(um))
      - 2.0*tmag*vpo*dumz*umz*N.x*(sym == 0)
    )
    + HomBoundaryConditions(sym, dum);

// TRESSIAN OPERATOR
  varf vT(defu(dum), defu(v))
    = HomBoundaryConditions(sym, dum);
