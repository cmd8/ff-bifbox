//
// modecompute.edp
// Chris Douglas
// chris.douglas@ladhyx.polytechnique.fr
//
// EXAMPLE USAGE:
//  Compute eigenvalues, store specrum without storing eigenvectors:
// mpirun -n 4 FreeFem++-mpi -v 0 modecompute.edp -eps_target 0.1+1.0i -eps_nev 10 -fi <FILEIN> -so <VALFILE>
//
//  Compute eigenvalues, store spectrum with eigenvectors:
// mpirun -n 4 FreeFem++-mpi -v 0 modecompute.edp -eps_target 0.1+1.0i -eps_nev 4 -fi <FILEIN> -fo <VALFILE>
//
// NOTE: This file should not be changed unless you know what you're doing.
//
load "iovtk"
load "PETSc-complex"
include "settings.idp"
include "macros_bifbox.idp"
// arguments
string meshin = getARGV("-mi", ""); // input meshfile
string filein = getARGV("-fi", "");
string fileout = getARGV("-fo", "");
string evalout = getARGV("-so", fileout);
bool adj = getARGV("-adj",0);
int sym = getARGV("-sym", 0);
string epstarget = getARGV("-eps_target", "0+0i");
int epsnev = getARGV("-eps_nev", 1);
bool strictnev = getARGV("-strict", 0);
string sttype = getARGV("-st_type", "sinvert");
string stpctype = getARGV("-st_pc_type", "lu");

string fileroot;
string fileext = parsefilename(filein, fileroot); //extract file name and extension
parsefilename(evalout, evalout); // trim extension from output file, if given
parsefilename(fileout, fileout); // trim extension from output file, if given
if(fileext == "mode" || fileext == "rslv"){
  filein = readbasename(workdir + filein);
  fileext = parsefilename(filein, fileroot);
}
if(filein != "" && meshin == "") meshin = readmeshname(workdir + filein); // get mesh file

// Load mesh, make FE basis
Th = readmeshN(workdir + meshin);
Thg = Th;
buildDmesh(Th);
restu = restrict(XMh, XMhg, n2o);
XMh defu(ub), defu(um2), defu(um3);
if(fileext == "base") {
  ub[] = loadbase(fileroot, meshin);
}
else if(fileext == "fold") {
  real[string] alpha;
  real beta;
  real[int] qm, qma;
  ub[] = loadfold(fileroot, meshin, qm, qma, alpha, beta);
}
else if(fileext == "hopf") {
  int sym;
  real omega;
  complex[string] alpha;
  complex beta;
  complex[int] qm, qma;
  ub[] = loadhopf(fileroot, meshin, qm, qma, sym, omega, alpha, beta);
}
else if(fileext == "foho") {
  int sym;
  real omega;
  complex[string] alpha1;
  real[string] alpha2;
  complex beta1, gamma12, gamma13;
  real beta22, beta23, gamma22, gamma23;
  complex[int] q1m, q1ma;
  real[int] q2m, q2ma;
  ub[] = loadfoho(fileroot, meshin, q1m, q1ma, q2m, q2ma, sym, omega, alpha1, alpha2, beta1, beta22, beta23, gamma12, gamma13, gamma22, gamma23);
}
else if(fileext == "hoho") {
  int sym1, sym2;
  real omega1, omega2;
  complex[string] alpha1, alpha2;
  complex beta1, beta2, gamma1, gamma2, gamma12, gamma13, gamma22, gamma23;
  complex[int] q1m, q1ma, q2m, q2ma;
  ub[] = loadhoho(fileroot, meshin, q1m, q1ma, q2m, q2ma, sym1, sym2, omega1, omega2, alpha1, alpha2, beta1, beta2, gamma1, gamma2, gamma12, gamma13, gamma22, gamma23);
}
else if(fileext == "tdns") {
  real time;
  ub[] = loadtdns(fileroot, meshin, time);
}
complex[int] val(epsnev);
XMh<complex>[int] defu(vec)(epsnev);
XMh<complex> defu(um);

Mat<complex> J;
createMatu(Th, J, Pk);
Mat<complex> M(J);
complex ik, ik2 = 0.0, ik3 = 0.0, iomega = 0.0, iomega2 = 0.0, iomega3 = 0.0; // Let PETSc/SLEPc do the shift
include "eqns.idp"

{
  ik = 1i*real(sym);
  {
    M = vM(XMh, XMh, tgv = -10);
    matrix<complex> Jtemp = vJ(XMh, XMh, tgv = -1);
    if (adj) Jtemp = -Jtemp';
    else Jtemp = -Jtemp;
    J = Jtemp;
  }
  int k = EPSSolve(J, M, vectors = vec, values = val,
                 sparams = " -st_type " + sttype
                         + " -st_pc_type " + stpctype
                         + " -eps_target " + epstarget
                         + " -eps_monitor_conv " + EPSparams);
  if (strictnev) {// activate to limit number of eigenpairs
    val.resize(min(k, epsnev));
    vec.resize(min(k, epsnev));
  }
  if(fileout != ""){
    for (int ii = 0; ii < val.n; ++ii){
      um[] = vec[ii][];
      complex[int] temp;
      ChangeNumbering(J, um[], temp);
      ChangeNumbering(J, um[], temp, inverse = true);
      vec[ii][] = um[];
    }
  }
  savemode(fileout, evalout, filein, meshin, vec, val, sym, (fileout != ""));
}
